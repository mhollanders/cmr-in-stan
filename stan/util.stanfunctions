/** 
 * Get first and last survey of detection for detection history. Function is 
 * overloaded for single survey and robust design.
 *
 * @param y  Detection history
 *
 * @return  First and last detection per individual
 */
array[,] int first_last(data array[,] int y) {
  int I = size(y), J = size(y[1]);
  array[I, 2] int f_l;
  for (i in 1:I) {
    for (j in 1:J) {
      if (y[i, j]) {
        f_l[i] = rep_array(j, 2);
        break;
      }
    }
    int JJ = J - f_l[i, 1];
    if (JJ) {
      array[JJ] int idx = linspaced_int_array(JJ, f_l[i, 1] + 1, J);
      for (j in reverse(idx)) {
        if (y[i, j]) {
          f_l[i, 2] = j;
          break;
        }
      }
    }
  }
  return f_l;
}
array[,] int first_last(data array[,,] int y) {
  int I = size(y), J = size(y[1]);
  array[I, 2] int f_l;
  for (i in 1:I) {
    for (j in 1:J) {
      if (max(y[i, j])) {
        f_l[i] = rep_array(j, 2);
        break;
      }
    }
    int JJ = J - f_l[i, 1];
    if (JJ) {
      array[JJ] int idx = linspaced_int_array(JJ, f_l[i, 1] + 1, J);
      for (j in reverse(idx)) {
        if (max(y[i, j])) {
          f_l[i, 2] = j;
          break;
        }
      }
    }
  }
  return f_l;
}

/** 
 * Get secondary of first detection in survey of first detection for robust
 * design Cormack-Jolly-Seber
 *
 * @param y  Detection history
 * @param f  First survey detected
 *
 * @return  First secondary detected in first survey
 */
array[] int first_sec(data array[,,] int y, data array[] int f) {
  int I = size(y), K = size(y[1, 1]);
  array[I] int f_k = zeros_int_array(I);
  for (i in 1:I) {
    for (k in 1:K) {
      if (y[i, f[i], k]) {
        f_k[i] = k;
        break;
      }
    }
  }
  return f_k;
}

/** 
 * Sample latent discrete parameters
 *
 * @param A  Vector of unnormalised (log) probabilities
 * @return   Categorical outcome
 */
int latent_rng(vector A) {
  return categorical_rng(softmax(A));
  // return categorical_logit_rng(A);
}

/**
 * Elementwise natural logarithm of the product of the elementwise 
 * exponentiation of two matrices
 *
 * @param A  First matrix or (row-)vector
 * @param B  Second matrix or (row-)vector
 *
 * @return   log(exp(A) * exp(B))
 */
matrix log_prod_exp(matrix A, matrix B) {
  int I = rows(A);
  int J = cols(A);
  int K = cols(B);
  matrix[J, I] A_tr = A';
  matrix[I, K] C;
  for (k in 1:K) {
    for (i in 1:I) {
      C[i, k] = log_sum_exp(A_tr[:, i] + B[:, k]);
    }
  }
  return C;
}
vector log_prod_exp(matrix A, vector B) {
  int I = rows(A);
  int J = cols(A);
  matrix[J, I] A_tr = A';
  vector[I] C;
  for (i in 1:I) {
    C[i] = log_sum_exp(A_tr[:, i] + B);
  }
  return C;
}
row_vector log_prod_exp(row_vector A, matrix B) {
  int K = cols(B);
  vector[size(A)] A_tr = A';
  row_vector[K] C;
  for (k in 1:K) {
    C[k] = log_sum_exp(A_tr + B[:, k]);
  }
  return C;
}
real log_prod_exp(row_vector A, vector B) {
  return log_sum_exp(A' + B);
}

/** 
 * Create transition rate matrix without absorbing states from transition 
 * and mortality rates
 *
 * @param h  Mortality rates per state
 * @param q  Non-mortality transition rates between states
 *
 * @return   Transition rate matrix
 */
matrix rate_matrix(vector h, row_vector q) {
  int S = size(h), Sm_1 = S - 1;
  matrix[S, S] Q = diag_matrix(-h);
  row_vector[Sm_1] q_s;
  int ss = 1;
  for (s in 1:S) {
    q_s = segment(q, ss, Sm_1);
    Q[s, 1:s - 1] = head(q_s, s - 1);
    Q[s, s] -= sum(q_s);
    Q[s, s + 1:S] = tail(q_s, S - s);
    ss += Sm_1;
  }
  return Q;
}
